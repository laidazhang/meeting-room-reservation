apply from: file("${rootDir}/gradle/project.gradle")

subprojects {
    group = 'core.demo'
    version = '1.0.0'

    repositories {
        maven {
            url 'https://neowu.github.io/maven-repo/'
            content {
                includeGroupByRegex 'core\\.framework.*'
            }
        }
    }
}

def coreNGVersion = '7.3.0'
def hsqlVersion = '2.5.0'

configure(subprojects.findAll { it.name.endsWith('-db-migration') }) {
    apply from: file("${rootDir}/gradle/db-migration.gradle")

    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
    }
}

configure(subprojects.findAll { it.name.endsWith('-mongo-migration') }) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
    }
    tasks.register('mongoMigrate') {
        dependsOn run
    }
}

configure(subprojects.findAll { it.name.matches('.*-interface') }) {
    dependencies {
        implementation "core.framework:core-ng-api:${coreNGVersion}"
    }
}

configure([project(':user-service'),
           project(':reservation-service'),
           project(':reservation-scheduler'),
           project(':reservation-back-office'),
           project(':reservation-website')]) {
    apply from: file("${rootDir}/gradle/app.gradle")
    dependencies {
        implementation "core.framework:core-ng:${coreNGVersion}"
        testImplementation "core.framework:core-ng-test:${coreNGVersion}"
    }
}

project(':user-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
        implementation project(':user-service-interface')
    }
}

project(':reservation-service') {
    dependencies {
        runtimeOnly 'mysql:mysql-connector-java:8.0.17'
        testRuntimeOnly "org.hsqldb:hsqldb:${hsqlVersion}"
        implementation "core.framework:core-ng-mongo:${coreNGVersion}"
        implementation project(':reservation-service-interface')
    }
}

project(':reservation-scheduler') {
    dependencies {
        implementation project(':reservation-service-interface')
        implementation project(':reservation-notify-service-interface')
    }
}

configure([project(':reservation-back-office'),
           project(':reservation-website')]) {
    dependencies {
        implementation project(':user-service-interface')
        implementation project(':reservation-service-interface')
    }
}


